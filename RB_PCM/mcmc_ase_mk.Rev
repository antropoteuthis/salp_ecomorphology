################################################################################
#
# RevBayes Example: Inferring ancestral states and rates of morphological evolution
# 
# This file: Runs the full MCMC on an Mk model.
#
# authors:  Sebastian Höhna, Will Freyman, April M. Wright, Michael J. Landis
#
################################################################################

#######################
# Reading in the Data #
#######################

morpho <- readDiscreteCharacterData("morph.nex")
moves    = VectorMoves()
monitors = VectorMonitors()
phylogeny <- readTrees("GUCMmNSanger2_TimeTree_salp18Sphylo.tre")[1]

###########################################
#     Morphological substitution model    #
###########################################

# Create the Q matrix. 

#T<>H
th ~ dnExponential( 1 )
th.setValue( 1 )
ht ~ dnExponential( 1 )
ht.setValue( 1 )

#T<>C<>W
cw ~ dnExponential( 1 )
cw.setValue( 1 )

#unused C<>W
wc ~ dnExponential( 1 )
wc.setValue( 1 )

#unused T<>W
wt ~ dnExponential( 1 )
wt.setValue( 1 )
tw ~ dnExponential( 1 )
tw.setValue( 1 )

#T<>O<>L
to ~ dnExponential( 1 )
to.setValue( 1 )

#unused T<>o
ot ~ dnExponential( 1 )
ot.setValue( 1 )

#unused O<>L
ol ~ dnExponential( 1 )
ol.setValue( 1 )

lo ~ dnExponential( 1 )
lo.setValue( 1 )

#L<>B
lb ~ dnExponential( 1 )
lb.setValue( 1 )

#unused B<>L
bl ~ dnExponential( 1 )
bl.setValue( 1 )

rates := [ [ 0.0, cw, 0.0, 0.0, 0.0, 0.0, 0.0 ],
           [ wc, 0.0, wt, 0.0, 0.0, 0.0, 0.0 ],
           [ 0.0, tw, 0.0, to, 0.0, 0.0, th ], 
           [ 0.0, 0.0, ot, 0.0, ol, 0.0, 0.0 ], 
           [ 0.0, 0.0, lo, 0.0, 0.0, lb, 0.0 ],
           [ 0.0, 0.0, 0.0, 0.0, bl, 0.0, 0.0 ], 
           [ 0.0, 0.0, ht, 0.0, 0.0, 0.0, 0.0 ] ]

Q_morpho := fnFreeK(rates)
P <- Q_morpho.getTransitionProbabilities(rate=0.1)
P

# Moves on the parameters to the Gamma distribution.
moves.append( mvScale(cw, weight=2.0) )
moves.append( mvScale(wc, weight=2.0) )
moves.append( mvScale(wt, weight=2.0) )
moves.append( mvScale(tw, weight=2.0) )
moves.append( mvScale(to, weight=2.0) )
moves.append( mvScale(ot, weight=2.0) )
moves.append( mvScale(ol, weight=2.0) )
moves.append( mvScale(lo, weight=2.0) )
moves.append( mvScale(lb, weight=2.0) )
moves.append( mvScale(bl, weight=2.0) )
moves.append( mvScale(th, weight=2.0) )
moves.append( mvScale(ht, weight=2.0) )


# Combine all of our elements into a CTMC.

phyMorpho ~ dnPhyloCTMC(tree=phylogeny, Q=Q_morpho, type="Standard")
phyMorpho.clamp(morpho)

########
# MCMC #
########

# initialize the model object #

mymodel = model(phylogeny)

# Create a vector of monitors #
# 1. for the full model #

monitors.append( mnModel(filename="morph_output/morph.log", printgen=1) )

# 2. and a few select parameters to be printed to the screen #

monitors.append( mnScreen(printgen=100) )

# 3. add an ancestral state monitor

monitors.append( mnJointConditionalAncestralState(tree=phylogeny,
                                                  ctmc=phyMorpho,
                                                  filename="morph_output/morph.states",
                                                  type="Standard",
                                                  printgen=1,
                                                  withTips=true,
                                                  withStartStates=false) )

# Initialize the MCMC object #

mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC #

mymcmc.run(generations=5000, tuningInterval=200)

# check the performance of the MCMC/moves

mymcmc.operatorSummary()

# Read in the tree trace and construct the ancestral states (ASE) #

anc_states = readAncestralStateTrace("morph_output/morph.states")

anc_tree = ancestralStateTree(tree=phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file="morph_output/ase_morph.tree", burnin=0.25, summary_statistic="MAP", site=1)

writeNexus( anc_tree, filename="morph_output/ase_morph.tree" )

# Quit RevBayes #
q()
