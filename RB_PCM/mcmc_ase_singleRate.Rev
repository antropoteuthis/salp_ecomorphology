################################################################################
#
# RevBayes Example: Inferring ancestral states and rates of morphological evolution
# 
# This file: Runs the full MCMC on an Mk model.
#
# authors:  Sebastian HÃ¶hna, Will Freyman, April M. Wright, Michael J. Landis
#
################################################################################

#######################
# Reading in the Data #
#######################

morpho <- readDiscreteCharacterData("morph.nex")
moves    = VectorMoves()
monitors = VectorMonitors()
phylogeny <- readTrees("GUCMmNSanger2_TimeTree_salp18Sphylo.tre")[1]

###########################################
#     Morphological substitution model    #
###########################################

# Create the Q matrix. 

#T<>H
R ~ dnExponential( 1 )
R.setValue( 1 )


rates := [ [ 0.0, R, 0.0, 0.0, 0.0, 0.0, 0.0 ],
           [ R, 0.0, R, 0.0, 0.0, 0.0, 0.0 ],
           [ 0.0, R, 0.0, R, 0.0, 0.0, R ], 
           [ 0.0, 0.0, R, 0.0, R, 0.0, 0.0 ], 
           [ 0.0, 0.0, R, 0.0, 0.0, R, 0.0 ],
           [ 0.0, 0.0, 0.0, 0.0, R, 0.0, 0.0 ], 
           [ 0.0, 0.0, R, 0.0, 0.0, 0.0, 0.0 ] ]

Q_morpho := fnFreeK(rates)
P <- Q_morpho.getTransitionProbabilities(rate=0.1)
P

# Moves on the parameters to the Gamma distribution.
moves.append( mvScale(R, weight=2.0) )


# Combine all of our elements into a CTMC.

phyMorpho ~ dnPhyloCTMC(tree=phylogeny, Q=Q_morpho, type="Standard")
phyMorpho.clamp(morpho)

########
# MCMC #
########

# initialize the model object #

mymodel = model(phylogeny)

# Create a vector of monitors #
# 1. for the full model #

monitors.append( mnModel(filename="morph_output/morph_single.log", printgen=1) )

# 2. and a few select parameters to be printed to the screen #

monitors.append( mnScreen(printgen=100) )

# 3. add an ancestral state monitor

monitors.append( mnJointConditionalAncestralState(tree=phylogeny,
                                                  ctmc=phyMorpho,
                                                  filename="morph_output/morph_single.states",
                                                  type="Standard",
                                                  printgen=1,
                                                  withTips=true,
                                                  withStartStates=false) )

# Initialize the MCMC object #

mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC #

mymcmc.run(generations=5000, tuningInterval=200)

# check the performance of the MCMC/moves

mymcmc.operatorSummary()

# Read in the tree trace and construct the ancestral states (ASE) #

anc_states = readAncestralStateTrace("morph_output/morph_single.states")

anc_tree = ancestralStateTree(tree=phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file="morph_output/ase_morph_single.tree", burnin=0.25, summary_statistic="MAP", site=1)

writeNexus( anc_tree, filename="morph_output/ase_morph_single.tree" )

# Quit RevBayes #
q()
