#######################
# Reading in the Data #
#######################

data = readDiscreteCharacterData("GovUNIONCho-AB921975Mman+news_MUSCLE.afa")
taxa <- data.taxa()
n_species <- data.ntaxa()
n_branches <- 2 * n_species - 3
mvi = 0
mni = 0

moves    = VectorMoves()
monitors = VectorMonitors()

##################################
# Substitution Model #
##################################

er_prior <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_prior)
moves.append(mvSimplexElementScale(er, alpha=10, tune=true, weight=3))
pi_prior <- v(1,1,1,1)
pi ~ dnDirichlet(pi_prior)
moves.append(mvSimplexElementScale(pi, alpha=10, tune=true, weight=2))

Q := fnGTR(er,pi)
alpha_prior_mean <- 5.0
alpha_prior_sd <- 0.587405

alpha ~ dnLognormal(alpha_prior_mean, alpha_prior_sd)
gamma_rates := fnDiscretizeGamma(alpha, alpha, 4, false)
moves.append(mvScale(alpha,weight=2))

pinvar ~ dnBeta(1,1)
moves.append(mvScale(pinvar, lambda=0.1, tune=true, weight=2.0))
moves.append(mvSlide(pinvar, delta=0.1, tune=true, weight=2.0))

##############
# Tree model #
##############

topology ~ dnUniformTopology(taxa=taxa)
moves.append(mvNNI(topology, weight=1.0))
moves.append(mvSPR(topology, weight=1.0))

for (i in 1:n_branches) {
	br_lens[i] ~ dnExponential(10.0)
	moves.append(mvScale(br_lens[i]))
}

TL := sum(br_lens)

# unite topology and branch length vector into phylogeny object
phylogeny := treeAssembly(topology, br_lens)

###################
# PhyloCTMC Model #
###################

phyloSeq ~ dnPhyloCTMC(tree=phylogeny, Q=Q, siteRates=gamma_rates, pInv=pinvar, type="DNA")
phyloSeq.clamp(data)

############
# Analysis #
############

mymodel = model(phylogeny)
# add monitors
monitors.append(mnModel(filename="TOPOLOGY_Gov2011+HigaCint+New_MUSCLE_output/Gov2011+HigaCint+New_MUSCLE_18S.log",printgen=10))
monitors.append(mnFile(phylogeny, filename="TOPOLOGY_Gov2011+HigaCint+New_MUSCLE_output/Gov2011+HigaCint+New_MUSCLE_18S.trees", printgen=100))
monitors.append(mnScreen(alpha,printgen=100))

# run the analysis
mymcmc = mcmc(mymodel, moves, monitors)
mymcmc.burnin(10000,1000)
mymcmc.run(300000)

# summarize output
treetrace = readTreeTrace("TOPOLOGY_Gov2011+HigaCint+New_MUSCLE_output/Gov2011+HigaCint+New_MUSCLE_18S.trees")
treetrace.summarize()
map_tree = mapTree(treetrace,"TOPOLOGY_Gov2011+HigaCint+New_MUSCLE_output/Gov2011+HigaCint+New_MUSCLE_18S.tre")
map_tree = consensusTree(trace=treetrace, cutoff=0.5, positiveBranchLengths=TRUE, file="TOPOLOGY_Gov2011+HigaCint+New_MUSCLE_output/Gov2011+HigaCint+New_MUSCLE_18S.cons.tree")

q()